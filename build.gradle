buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}
apply plugin: "net.minecraftforge.gradle.forge"
apply from: "build-common.gradle"

sourceCompatibility = targetCompatibility = 1.8

group = "${project.package_base}"
archivesBaseName = "${project.name}-${project.mc_version}"
version = (project.prerelease != "") ? "${project.mod_version}-${project.prerelease}" : "${project.mod_version}"

repositories {
    maven { url "https://dl.bintray.com/shinoow/maven/" } //AbyssalCraft
    maven { url "https://files.minecraftforge.net/maven" } //BiomesOPlenty
}

dependencies {
    provided "com.shinoow.abyssalcraft:AbyssalCraft:${ac_ver}:api"
    provided "com.github.glitchfiend.biomesoplenty:BiomesOPlenty:${bop_ver}:deobf"
}

minecraft {
    setVersion "${project.mc_version}-${project.mcf_version}"
    setMappings "${project.mcp_mappings}"
    setMakeObfSourceJar false
    setRunDir "run"
    replace "@MOD_VERSION@", "${project.version}"
    replace "@API_VERSION@", "${project.api_version}"
    replace "0.0-MCF+MINVER", "${project.mcf_minver}"
    if (project.depstring != "") {replace("after:MODDEPS", "${project.depstring}")} else {replace "after:MODDEPS", ""}
    replaceIn "RTG.java"
    replaceIn "RTGAPI.java"
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
		expand ([
			"version"  :mod_version,
			"mcversion":mc_version
		])
	}
	from(sourceSets.main.resources.srcDirs) {exclude "mcmod.info"}
}

jar {
    manifest { attributes(["Maven-Artifact": "${project.group}:${archivesBaseName}:${project.version}"]) }
}
